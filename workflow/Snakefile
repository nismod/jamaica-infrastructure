import pandas


# Default config
# - to override, either edit the file in place, or create a config_local.yml
# with custom values and run:
#    snakemake --cores=1 --configfile=config_local.yml all
configfile: "config.yml"


hazard_layers = pandas.read_csv(config["hazard_layers"])
network_layers = pandas.read_csv(config["network_layers"])
bbox = config['extract_bbox']

rule extract:
    input:
        [f"networks/{path}" for path in network_layers.path.unique()],
        list(hazard_layers.path)


rule extract_vector_bbox:
    input:
        f"{config['paths']['processed_data']}/{{path}}.gpkg"
    output:
        f"{config['paths']['processed_data']}/extract/{{path}}.gpkg"
    params:
        left=bbox['left'],
        right=bbox['right'],
        top=bbox['top'],
        bottom=bbox['bottom'],
    shell:
        """
        # clipsrc needs: xmin ymin xmax ymax
        ogr2ogr -clipsrc {params.left} {params.bottom} {params.right} {params.top}  {output} {input}
        """


rule extract_raster_bbox:
    input:
        f"{config['paths']['processed_data']}/{{path}}.tif"
    output:
        f"{config['paths']['processed_data']}/extract/{{path}}.tif"
    params:
        left=bbox['left'],
        right=bbox['right'],
        top=bbox['top'],
        bottom=bbox['bottom'],
    shell:
        """
        # target extent (-te) needs: xmin ymin xmax ymax
        gdalwarp -te {params.left} {params.bottom} {params.right} {params.top} {input} {output}
        """
